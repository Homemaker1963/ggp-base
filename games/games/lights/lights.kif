; There are three light (left, middle, right) and three corresponding buttons.
; Pressing a button toggles its light.  Turn on all the lights.

; Setup {{{
(role you)

(init (light left off))
(init (light middle off))
(init (light right off))
(init (turn 0))

(always) ; work around broken gamestepper

; }}}
; Moves {{{
(<= (legal you (press left)) (always))
(<= (legal you (press middle)) (always))
(<= (legal you (press right)) (always))
(<= (legal you wait) (always))

; }}}
; State {{{

(<= (next (light ?pos on))
  (true (light ?pos on))
  (not (does you (press ?pos))))

(<= (next (light ?pos on))
  (true (light ?pos off))
  (does you (press ?pos)))

(<= (next (light ?pos off))
  (true (light ?pos off))
  (not (does you (press ?pos))))

(<= (next (light ?pos off))
  (true (light ?pos on))
  (does you (press ?pos)))

(<= (next (turn ?x))
  (true (turn ?current))
  (succ ?current ?x))

; }}}
; Ending {{{
(<= a-light-off
  (true (light ?position off)))

(<= terminal
  (not a-light-off))

(<= terminal
  (true (turn 7)))

; }}}
; Goals {{{
(<= (goal you 100)
  (not a-light-off))

(<= (goal you 0)
  a-light-off)

; }}}
; Math {{{
(succ 0 1)
(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)
(succ 9 10)
; }}}

